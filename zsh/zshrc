export ZSH=$HOME/.dotfiles
export PROJECTS=~/code

# Stash of environment variables that are kept out of version control
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files
export RBENV_ROOT=$HOME/.rbenv
eval "$(rbenv init -)"

# The next line updates PATH for the Google Cloud SDK.
if [ -f /Users/ben.haan/Downloads/google-cloud-sdk/path.zsh.inc ]; then
  source '/Users/ben.haan/Downloads/google-cloud-sdk/path.zsh.inc'
fi

# The next line enables shell command completion for gcloud.
if [ -f /Users/ben.haan/Downloads/google-cloud-sdk/completion.zsh.inc ]; then
  source '/Users/ben.haan/Downloads/google-cloud-sdk/completion.zsh.inc'
fi
